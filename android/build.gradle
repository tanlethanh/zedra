buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    mavenCentral()
}

android {
    namespace='dev.zedra.app'
    compileSdk 34

    defaultConfig {
        applicationId "dev.zedra.app"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "0.1"
    }

    buildTypes {
        release {
            minifyEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'app/src/main/AndroidManifest.xml'
            java.srcDirs = ['app/src/main/java']
            resources.srcDirs = ['app/src/main/resources']
            res.srcDirs = ['app/src/main/res']
            jniLibs.srcDirs = ['app/libs']
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // CameraX for QR scanning
    def camerax_version = "1.4.0"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

    // ML Kit barcode scanning
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'

    // Lifecycle (needed for CameraX)
    implementation 'androidx.lifecycle:lifecycle-runtime:2.7.0'
}

task buildRustLib(type: Exec) {
    workingDir '..'
    commandLine './scripts/build-android.sh'
}

preBuild.dependsOn buildRustLib
